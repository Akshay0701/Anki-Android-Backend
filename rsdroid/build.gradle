apply plugin: 'com.android.library' // required for aar generation to link to from AnkiDroid
apply plugin: 'com.google.protobuf'
apply plugin: 'maven-publish'

// TODO: Extract versionName/version for publishing to variable

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"
    ndkVersion "21.3.6528147" // Older version, but used by GitHub actions

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "0.0.1"

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

// Consider upgrade to DSL: https://docs.gradle.org/current/userguide/plugins.html#sec:plugins_block
apply plugin: "org.mozilla.rust-android-gradle.rust-android"

cargo {
    module = "../rslib-bridge"
    libname = "rsdroid"
    targets = ["x86", "arm", "x86_64", "arm64"]
    profile = 'release'
    apiLevel = 21
    verbose = true
}

apply from: "proto.gradle"

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar", '*.so'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    // Protobuf is part of the ABI, so include it as a compile/api dependency.
    api 'com.google.protobuf:protobuf-java:3.13.0'
    implementation 'androidx.sqlite:sqlite:2.1.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.3.1"
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation project(path: ':rsdroid-testing')

}

preBuild.dependsOn "cargoBuild"

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release
                groupId = 'com.github.david-allison-1'
                artifactId = 'anki-android-backend'
                version = "0.0.1"
            }
        }
    }
}